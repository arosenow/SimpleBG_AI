
//Note
//if a check succeeds it disables the current modal so we have to avoid successful checks except combat 

IF
	Global("BEPASSIVE","LOCALS",0)
	HotKey(V)
THEN
	RESPONSE #100
		SetGlobal("BEPASSIVE","LOCALS",1)
		DisplayStringHead(Myself,95137)  // Not attacking enemies
		ClearActions()
		SetGlobal("IMHELPING","LOCALS",0)
END

IF
	Global("BEPASSIVE","LOCALS",1)
	HotKey(V)
THEN
	RESPONSE #100
		SetGlobal("BEPASSIVE","LOCALS",0)
		DisplayStringHead(Myself,95135)  // Attacking enemies
END

IF
	OR(3)
		ModalState(STEALTH)
		StateCheck(Myself,STATE_INVISIBLE)
		StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN
	RESPONSE #100
END

//////////////////////////////////////////////////////////////////////////
// Fighters can't activate anything, so make them keep fighting forever
IF
	DELAY(12)
	ActionListEmpty()
	!ButtonDisabled(BUTTON_DIALOG)
	OR(2)
		!CombatCounter(0)
		Exists([Enemy])
	OR(3)
		!ButtonDisabled(BUTTON_TURNUNDEAD) //Turn undead
		!ButtonDisabled(BUTTON_THIEVING) //Thieving
		!ButtonDisabled(BUTTON_BATTLESONG) //Bardsong
THEN
	RESPONSE #100
    SetGlobal("SKILLTIME","LOCALS",1)
	SetGlobal("IMHELPING","LOCALS",0)
	StartTimer(111,3)
	Continue()
END

IF
	Global("SKILLTIME","LOCALS",1)
	TimerExpired(111)
	OR(2)
		!CombatCounter(0)
		Exists([Enemy])
	ActionListEmpty()
THEN
	RESPONSE #100
    SetGlobal("SKILLTIME","LOCALS",0)
	Continue()
END

////////////////////////////////////////////////////////////////////////////////////
//See who is near you
IF
	!See(NearestEnemyOf(Myself))
	!See(SecondNearestEnemyOf(Myself))
	!See(ThirdNearestEnemyOf(Myself))
	!See(FourthNearestEnemyOf(Myself))
	!See(FifthNearestEnemyOf(Myself))
	!See(SixthNearestEnemyOf(Myself))
	!See([ENEMY])
	FALSE()
THEN
	RESPONSE #100
END

/////////////////////////////////////////////////////////////////////////////////////////

//Fight nearby people 
IF
	Global("BEPASSIVE","LOCALS",0)
	OR(2)
		Global("IMHELPING","LOCALS",1)
		ActionListEmpty()
	!IsWeaponRanged(Myself)
	!ModalState(SHAMANDANCE)
	CheckStatLT(Myself,21,THAC0)
	!TargetUnreachable(LastSeenBy(Myself))
	!Allegiance(LastSeenBy(Myself),GOODCUTOFF) 
	!Allegiance(LastSeenBy(Myself),ALLY) 
	!Allegiance(LastSeenBy(Myself),NEUTRAL) 
	!InParty(LastSeenBy(Myself))
	!Class(LastSeenBy(Myself),INNOCENT)
	LOS(LastSeenBy(Myself),7)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		help()
		SetGlobal("IMHELPING","LOCALS",0)
		SetInterrupt(TRUE)
		AttackReevaluate(LastSeenBy(Myself),3)
END

//Attack with your bow if equipped
IF
	Global("BEPASSIVE","LOCALS",0)
	Global("SKILLTIME","LOCALS",0)
	OR(2)
		Global("IMHELPING","LOCALS",1)
		ActionListEmpty()
	LOS(LastSeenBy(Myself),30)
	!TargetUnreachable(LastSeenBy(Myself))
	IsWeaponRanged(Myself)
	!ModalState(SHAMANDANCE)
	!TargetUnreachable(LastSeenBy(Myself))
	!InParty(LastSeenBy(Myself))
	!Allegiance(LastSeenBy(Myself),GOODCUTOFF) 
	!Allegiance(LastSeenBy(Myself),ALLY) 
	!Allegiance(LastSeenBy(Myself),NEUTRAL) 
	!Class(LastSeenBy(Myself),INNOCENT)
	CHECKSTATGT(Myself,4,VISUALRANGE)
	CheckStatLT(Myself,21,THAC0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("IMHELPING","LOCALS",0)
		SetInterrupt(TRUE)
		Shout(1234)
		AttackReevaluate(LastSeenBy(Myself),3)
END


////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////
//If you're a competent archer help anyone who shouted out for help from a ranged opponent
IF
	Heard([PC],1234)
	IsWeaponRanged(Myself)
	Global("SKILLTIME","LOCALS",0)
	Global("BEPASSIVE","LOCALS",0)
	Global("IMHELPING","LOCALS",0)
	ActionListEmpty()
	HPPercentGT(Myself,25)
	CHECKSTATGT(Myself,4,VISUALRANGE)
	CheckStatLT(Myself,21,THAC0)
	OR(4)
		Class(Myself,PALADIN_ALL)
		Class(Myself,FIGHTER_ALL)
		Class(Myself,RANGER_ALL)
		Class(Myself,THIEF_ALL)
	!ModalState(SHAMANDANCE)
	!TargetUnreachable(LastHeardBy(Myself))
	Range(LastHeardBy(Myself),20)
	!Range(LastHeardBy(Myself),1)
THEN
	RESPONSE #100
		SetGlobal("IMHELPING","LOCALS",1)
		SetInterrupt(TRUE)
		MoveToObject(LastHeardBy(Myself))
END

// If you're a fighter or not a weakling and you have >50% of your health, help others
IF
	Global("IMHELPING","LOCALS",0)
	Global("SKILLTIME","LOCALS",0)
	Global("BEPASSIVE","LOCALS",0)
	ActionListEmpty()
	!Range(LastSeenBy(Myself),5)
	HPGT(Myself,6)
	HPPercentGT(Myself,50)
	CHECKSTATGT(Myself,4,VISUALRANGE)
	CheckStatLT(Myself,21,THAC0)
	Help([PC])
	OR(7)
		Class(Myself,PALADIN_ALL)
		Class(Myself,FIGHTER_ALL)
		Class(Myself,RANGER_ALL)
		Class(Myself,MONK)
		Class(Myself,THIEF_ALL)
		Class(Myself,CLERIC_ALL)
		Class(Myself,DRUID_ALL)
	!ModalState(SHAMANDANCE)
	!TargetUnreachable(LastHelp(Myself))
	!Range(LastHelp(Myself),3)
	Range(LastHelp(Myself),20)
THEN
	RESPONSE #100
		SetGlobal("IMHELPING","LOCALS",1)
		SetInterrupt(TRUE)
		MoveToObject(LastHelp(Myself))
END

/////////////////////////////////////////////////////////////////////////////////////////
// If you're using ranged weapons or are using a melee but aren't in combat, use your abilities
// A couple of timers here lets you alternate skills and combat optimally



//If you're a cleric and there's undead turn undead
IF
	ActionListEmpty()
	!ButtonDisabled(BUTTON_DIALOG)
	!ButtonDisabled(BUTTON_TURNUNDEAD)
	Exists([ENEMY.UNDEAD])
	!Exists([ALLY.UNDEAD])
	!Exists([NEUTRAL.UNDEAD])
	!ModalState(BATTLESONG)  
	!ModalState(TURNUNDEAD) 
	!ModalState(SHAMANDANCE)
	!ModalState(DETECTTRAPS)
	InParty(Myself)
	OR(2)
		Class(Myself,CLERIC_ALL)
		Class(Myself,PALADIN_ALL)
THEN
	RESPONSE #100
		Turn()
END

// Detect traps if you can
IF
	ActionListEmpty()
	!ButtonDisabled(BUTTON_THIEVING)
	!ButtonDisabled(BUTTON_DIALOG)
	!ModalState(BATTLESONG)   
	!ModalState(TURNUNDEAD) 
	!ModalState(SHAMANDANCE)
	!ModalState(DETECTTRAPS)
	InParty(Myself)
	OR(3)
		Class(Myself,THIEF_ALL)
		Class(Myself,MONK)
		Class(Myself,SHAMAN)
THEN
	RESPONSE #100
		FindTraps()
END

//If you're a bard try singing
IF
	ActionListEmpty()
	!ButtonDisabled(BUTTON_BATTLESONG)
	!ButtonDisabled(BUTTON_DIALOG)
	!ModalState(BATTLESONG)  
	!ModalState(TURNUNDEAD) 
	!ModalState(SHAMANDANCE)
	!ModalState(DETECTTRAPS)
	Class(Myself,BARD)
	NumInPartyAliveGT(2)
	OR(2)
		Exists([Enemy])
		!CombatCounter(0)
THEN
	RESPONSE #100
		BattleSong()
END


///////////////////////////////////////////////
// If you're being attacked from range call for help
IF
	!ModalState(SHAMANDANCE)
	AttackedBy([EVILCUTOFF],DEFAULT)
	!Range(LastSeenBy(Myself),5)
	!IsWeaponRanged(Myself)
	Global("SKILLTIME","LOCALS",0)
	ActionListEmpty()
	OR(2)
		!CombatCounter(0)
		Exists([Enemy])
THEN
	RESPONSE #100
	Shout(1234)
END